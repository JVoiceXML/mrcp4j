apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'directory-network', name: 'mina', version: '0.7.4'
    compile group: 'commons-logging', name: 'commons-logging', version: '1.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.11.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes("Implementation-Title": 'MRCPv2 library for Java',
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': version,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
    baseName 'org.mrcp4j'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { 
                MavenDeployment deployment -> signing.signPom(deployment) 
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: JVOICEXML_OSSRH_USERNAME, password: JVOICEXML_OSSRH_PASSWORD)
            }
    
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: JVOICEXML_OSSRH_USERNAME, password: JVOICEXML_OSSRH_PASSWORD)
            }
    
            pom.project {
                name 'JVoiceXML'
                groupId = JVOICEXML_GROUP
                packaging 'jar'
                description 'MRCP4J provides a Java API that encapsulates the MRCPv2 protocol and can be used to implement MRCP clients and/or servers.'
                url 'https://github.com/JVoiceXML/JVoiceXML'
                
                scm {
                    connection 'scm:git@github.com:JVoiceXML/mrcp4j.git'
                    developerConnection 'scm:git@github.com:JVoiceXML/mrcp4j.git'
                    url "https://github.com/JVoiceXML/JVoiceXML/tree/release/${version}"
                }
                    
                licenses {
                    license {
                        name 'GNU Lesser General Public License, Version 2.1'
                        url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }
    
                developers {
                    developer {
                        id 'schnelle'
                        name 'Dirk Schnelle-Walka'
                        email 'dirk.schnelle@jvoicexml.org'
                    }
                }
            }
        }
    }
}
